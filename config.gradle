/*
 * Copyright 2018 Keval Patel.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    versions = [
            'kotlin'        : '1.2.40',
            'buildTools'    : '27.0.3',
            'compileSdk'    : 27,
            'targetSdk'     : 27,
            'minSdk'        : 21,
            'supportLibrary': '27.1.1',
            'espresso'      : '3.0.1',
            'okHttp'        : "3.10.0",
            'retrofit'      : "2.3.0",
            'archComponent' : "1.1.0",
            'daggerVersion' : "2.15",
            'room'          : "1.0.0"
    ]

    packageExcludes = {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'protobuf.meta'
    }

    //Exclude group for dependency
    excludeGrp = {
        exclude group: 'com.android.support.test.espresso', module: 'espresso-core'
        exclude group: 'com.android.support.test.espresso', module: 'espresso-idling-resource'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-fragment'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'android.arch.lifecycle', module: 'common'
        exclude group: 'android.arch.core', module: 'common'
        exclude group: 'android.arch.core', module: 'common'
        exclude group: 'android.arch.lifecycle', module: 'runtime'
        exclude group: 'com.jakewharton.timber', module: 'timber'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }

    defaultAppConfig = {
        multiDexEnabled false

        applicationId "com.kevalpatel2106.ci.greenbuild"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode 1
        versionName "1.0"

        //strip other than english resources
        resConfigs "en"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    libDefaultConfig = {

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        //strip other than english resources
        resConfigs "en"

        //Versions config
        versionCode 1
        versionName "1.0"

        consumerProguardFiles '../proguard-rules.pro'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOption = {
        abortOnError true
        htmlReport true
        xmlReport true
        textReport true
        disable 'InvalidPackage'
        checkTestSources true
        ignoreWarnings true
        disable 'GradleCompatible'
    }

    compileOption = {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dokkaConfig = {
        outputFormat = 'javadoc'
        outputDirectory = "${project.rootDir}/docs"
        jdkVersion = 8 // Used for linking to JDK
        includeNonPublic = false
        skipDeprecated = false
        skipEmptyPackages = true
    }

    testOption = {
        unitTests.all {
            minHeapSize = "1024m"
            includeAndroidResources = true

            // set heap size for the test JVM(s)
            maxParallelForks 4
            maxHeapSize = "1596m"

            testLogging {
                events 'passed', 'skipped', 'failed'
            }
        }
    }

    jacocoAndroidUnitTestReportConfig = {
        csv.enabled true
        html.enabled true
        xml.enabled false
    }

    kotlinOption = {
        freeCompilerArgs = ['-Xno-param-assertions']
        allWarningsAsErrors = true
        jvmTarget = "1.8"
    }

    aaptOption = {
        cruncherProcesses 2
    }

    adbOption = {
        timeOutInMs 10_000
    }
}
